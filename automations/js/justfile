set dotenv-load := false

COLOR := "\\033[0;31m"
NO_COLOR := "\\033[0m"

# Show all available recipes
@_default:
    printf "\n{{ COLOR }}# Node.js automations (path: \`automations/js/\`)\n"
    printf "==============================================={{ NO_COLOR }}\n"
    just --list --unsorted

# Run a script
run script *args:
    pnpm --filter automations exec node src/{{ script }} {{ args }}

#################
# Weekly report #
#################

# Generate and post the weekly report
report:
    just run last_week_tonight.mjs

##########
# Render #
##########

render-release-drafter:
    #! /usr/bin/env sh
    render_release_drafter() {
        slug=$1
        name=$2
        just run render-jinja.js \
            templates/release-drafter.yml.jinja \
            .github/release-drafter-$slug.yml \
            "'{ \"app\": \"$name\" }'"
    }
    render_release_drafter api API
    render_release_drafter ingestion_server "Ingestion Server"
    render_release_drafter frontend Frontend
    render_release_drafter catalog Catalog

# Render all templates (shortcut for easy iteration)
render-templates:
    just render-release-drafter
