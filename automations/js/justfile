set dotenv-load := false

# Show all available recipes
@_default:
  echo "#######################"
  echo "# Node.js automations #"
  echo "#######################"
  just --list --unsorted


# Install dependencies
install:
  pnpm install

##########
# Render #
##########

# Run `render-jinja.js` with given input file, output file and context
render in_file out_file ctx="{}":
    node src/render-jinja.js {{ in_file }} {{ out_file }} {{ ctx }}

# Render `.pre-commit-config.yaml`
render-precommit:
    just render templates/.pre-commit-config.local.yaml.jinja .pre-commit-config.yaml

# Render `prettier.config.js`
render-prettier:
    just render templates/prettier.config.js.jinja prettier.config.js

# Render GitHub issue & PR templates
render-github:
    just render templates/pull_request_template.md.jinja .github/PULL_REQUEST_TEMPLATE/pull_request_template.md

# Render all templates (shortcut for easy iteration)
render-templates:
    just render-precommit
    just render-prettier
    just render-github
