set dotenv-load := false

COLOR := "\\033[0;31m"
NO_COLOR := "\\033[0m"

# Show all available recipes
@_default:
    printf "\n{{ COLOR }}# Node.js automations (path: \`automations/js/\`)\n"
    printf "==============================================={{ NO_COLOR }}\n"
    just --list --unsorted


#################
# Weekly report #
#################

# Generate and post the weekly report
report:
    node src/last_week_tonight.mjs

##########
# Render #
##########

# Run `render-jinja.js` with given input file, output file and context
render in_file out_file ctx="{}":
    node src/render-jinja.js {{ in_file }} {{ out_file }} '{{ ctx }}'

# Render `.pre-commit-config.yaml`
render-precommit:
    just render templates/.pre-commit-config.local.yaml.jinja .pre-commit-config.yaml

# Render `prettier.config.js`
render-prettier:
    just render templates/prettier.config.local.js.jinja prettier.config.js

# Render GitHub issue & PR templates
render-github:
    just render templates/pull_request_template.md.jinja .github/PULL_REQUEST_TEMPLATE/pull_request_template.md

render-release-drafter:
    just render templates/release-drafter.yml.jinja .github/release-drafter-api.yml '{ "app": "API" }'
    just render templates/release-drafter.yml.jinja .github/release-drafter-ingestion_server.yml '{ "app": "Ingestion server" }'
    just render templates/release-drafter.yml.jinja .github/release-drafter-frontend.yml '{ "app": "Frontend" }'
    just render templates/release-drafter.yml.jinja .github/release-drafter-catalog.yml '{ "app": "Catalog" }'

# Render all templates (shortcut for easy iteration)
render-templates:
    just render-precommit
    just render-prettier
    just render-github
    just render-release-drafter
