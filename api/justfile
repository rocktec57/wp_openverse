set dotenv-load := false

COLOR := "\\033[0;34m"
NO_COLOR := "\\033[0m"

# Show all available recipes
@_default:
    printf "\n{{ COLOR }}# API (path: \`api/\`)\n"
    printf "===================={{ NO_COLOR }}\n"
    just --list --unsorted


# Install dependencies
install *args="--dev":
    pipenv install {{ args }}

######
# Up #
######

# Bring up services specific to the API profile
up *flags:
    env COMPOSE_PROFILES="api" just ../up {{ flags }}

# Wait for all profile services to be up
wait-up: up
    just ../ingestion_server/wait # API profile includes ingestion server
    just wait # API waits for ES in entrypoint

# Load sample data into API via ingestion server
init: wait-up
    cd .. && ./load_sample_data.sh

##########
# Health #
##########

# Check the health of the service
@health host="localhost:50280":
    -curl -s -o /dev/null -w '%{http_code}' 'http://{{ host }}/healthcheck/'

# Wait for the service to be healthy
@wait host="localhost:50280":
    # The just command on the second line is executed in the context of the
    # parent directory and so must be prefixed with `api/`.
    just ../_loop \
    '"$(just api/health {{ host }})" != "200"' \
    "Waiting for the API to be healthy..."

########
# cURL #
########

# Make a cURL GET request to service at the given path
_curl-get path host="localhost:50280":
    curl "http://{{ host }}/v1/{{ path }}"

# Make a test cURL GET request to the API
stats media="images" host="localhost:50280":
    just _curl-get "{{ media }}/stats/" {{ host }}

# Launch a `pgcli` shell in the web container (requires typing credentials)
pgcli db_host="db" db_name="openledger" db_user="deploy":
    just ../exec web pgcli -h {{ db_host }} openledger

#########################
# Django administration #
#########################

# Run Django administrative commands locally
dj-local +args="":
    pipenv run python manage.py {{ args }}

# Run Django administrative commands inside the Docker container
dj +args="": wait-up
    just ../exec web python manage.py {{ args }}

# Get IPython shell inside the Docker container
ipython:
    just dj shell

# Get DB shell inside the Docker container
dbshell:
    just dj dbshell

# Run `collectstatic` to prepare for building the `nginx` Dockerfile target.
collectstatic:
    # The STATIC_ROOT environment variable is relative to the Django container's
    # default working directory i.e. the `/api/` directory. The resulting output
    # will be `/api/static/`, which is ignored by Git for convenience.
    STATIC_ROOT="./static" just dj collectstatic --noinput

#########
# Tests #
#########

# Run API tests inside the Docker container
test *args: wait-up
    just ../exec web ./test/run_test.sh {{ args }}

# Run API tests locally
test-local *args:
    pipenv run ./test/run_test.sh {{ args }}

# Run smoke test for the API docs
doc-test: wait-up
    curl --fail 'http://localhost:50280/v1/?format=openapi'
