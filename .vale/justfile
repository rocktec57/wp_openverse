COLOR := "\\033[0;34m"
NO_COLOR := "\\033[0m"

# Show all available recipes
@_default:
    printf "\n{{ COLOR }}# Vale (path: \`.vale/\`)\n"
    printf "===================={{ NO_COLOR }}\n"
    just --list --unsorted

# Build a local version of `openverse-vale:local` for testing
build:
    docker build . -t openverse-vale:local

IGNORE_FILES_PATTERN := ".pnpm|changelogs|projects/proposals|node_modules|.?venv"

# This recipe generates a list of Markdown and MDX files to be processed, excluding those
# matched by the IGNORE_FILES_PATTERN. Files are listed relative to the parent directory
# of the current working directory. If a custom separator is specified, newlines are
# replaced with this separator before outputting the file list.
@files separator="\n":
    #! /usr/bin/env sh
    files=$(
        find "$PWD/.." -type f \( -name "*.md" -o -name "*.mdx" \) \
            | grep -vE "{{ IGNORE_FILES_PATTERN }}" \
            | sed "s|$PWD/../||"
    )
    if [ '{{ separator }}' != '\n' ]; then
        echo "$files" | tr '\n' '{{ separator }}'
        exit 0
    fi
    echo "$files"

# Run Vale configured for Openverse in Docker.
# Using Docker avoids the need for contributors to install the Vale binary.
#Configuration defaults to what is used for CI.
run: build
    docker run --rm \
        -v $PWD/..:/src:rw,Z \
        --workdir=/src \
        openverse-vale:local \
        {{ "`just files`" }}
